buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
    }
}
apply plugin: 'com.android.application'
repositories {
    google()
    jcenter()
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    // Adds a prefix to the search path for bootstrap classes.
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs.add('-Xbootclasspath/p:frameworks-libs/framework-android-9.0.0_r37.jar')
        }
    }
}

static def getDate() {
    return new Date().format("YYMMdd_HHmm", TimeZone.getTimeZone("GMT"))
}

android {

    signingConfigs {
        system {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('keystore/aosp_platform_key.jks')
            storePassword 'android'
        }
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.ultracat.sample.pdkappsample"
        minSdkVersion 22
        targetSdkVersion 24
        def time = new Date().format("yyyyMMdd")
        def versionFromDate = Integer.valueOf(time)

        versionCode versionFromDate
        versionName "0.1-${getDate()}"
    }

    flavorDimensions "platform"

    productFlavors {
        prj1 {
            dimension "platform"
            versionName "${name}-${defaultConfig.versionName}"
            signingConfig signingConfigs.system
        }

        prj2 {
            dimension "platform"
            versionName "${name}-${defaultConfig.versionName}"
            signingConfig signingConfigs.system
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src/aidl']
        }
        prj2 {
            res.srcDirs = ['res-prj2']
        }
        prj1 {
            res.srcDirs = ['res']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.system
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "PdkAppSample-${variant.flavorName}_${variant.buildType.name}_${defaultConfig.versionName}.apk"
                }
            }
        }
        debug {
            signingConfig signingConfigs.system
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        //If you want to continue even if errors found use following line
        abortOnError false
    }
}

dependencies {
    compileOnly project(':frameworks-libs')
    implementation project(':my-libs')
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
}

preBuild {

    doLast {
        def imlFile = file(project.name + ".iml")
        println 'Change ' + project.name + '.iml order'
        try {
            def parsedXml = (new XmlParser()).parse(imlFile)
            def jdkNode = parsedXml.component[1].orderEntry.find { it.'@type' == 'jdk' }
            parsedXml.component[1].remove(jdkNode)
            def sdkString = "Android API " + android.compileSdkVersion.substring("android-".length()) + " Platform"
            new Node(parsedXml.component[1], 'orderEntry', ['type': 'jdk', 'jdkName': sdkString, 'jdkType': 'Android SDK'])
            groovy.xml.XmlUtil.serialize(parsedXml, new FileOutputStream(imlFile))
        } catch (FileNotFoundException e) {
            // nop, iml not found
            println "no iml found"
        }
    }
}